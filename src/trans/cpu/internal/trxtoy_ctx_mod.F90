! (C) Copyright 1995- ECMWF.
! (C) Copyright 1995- Meteo-France.
! 
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.
!

MODULE TRXTOY_CTX_MOD

USE PARKIND1, ONLY : JPIM

IMPLICIT NONE

TYPE TRXTOY_CTX
  INTEGER(KIND=JPIM) :: ISENDCOUNT = -9999
  INTEGER(KIND=JPIM) :: IRECVCOUNT = -9999
  INTEGER(KIND=JPIM) :: INSEND = -9999
  INTEGER(KIND=JPIM) :: INRECV = -9999
  INTEGER(KIND=JPIM), ALLOCATABLE :: ISENDTOT (:)
  INTEGER(KIND=JPIM), ALLOCATABLE :: IRECVTOT (:)
  INTEGER(KIND=JPIM), ALLOCATABLE :: ISEND    (:)
  INTEGER(KIND=JPIM), ALLOCATABLE :: IRECV    (:)
  INTEGER(KIND=JPIM), ALLOCATABLE :: IINDEX(:)
  INTEGER(KIND=JPIM), ALLOCATABLE :: INDOFF(:)
  INTEGER(KIND=JPIM), ALLOCATABLE :: IGPTRSEND(:,:,:)
  INTEGER(KIND=JPIM), ALLOCATABLE :: ISETAL(:), ISETBL(:), ISETWL(:), ISETVL(:)
  CONTAINS
    PROCEDURE :: ALLOCATE_CTX
END TYPE TRXTOY_CTX

TYPE TRXTOY_VARS
  INTEGER(KIND=JPIM), ALLOCATABLE :: IUVLEVS(:)
  INTEGER(KIND=JPIM), ALLOCATABLE :: IUVPARS(:)
  INTEGER(KIND=JPIM), ALLOCATABLE :: IGP2PARS(:)
  INTEGER(KIND=JPIM), ALLOCATABLE :: IFLDOFF(:)
  INTEGER(KIND=JPIM), ALLOCATABLE:: IGPTROFF(:)
  LOGICAL, ALLOCATABLE   :: LLUV(:)
  LOGICAL, ALLOCATABLE   :: LLGP2(:)
  LOGICAL, ALLOCATABLE   :: LLGP3A(:)
  LOGICAL, ALLOCATABLE   :: LLGP3B(:)
  INTEGER(KIND=JPIM), ALLOCATABLE  :: IGP3APARS(:)
  INTEGER(KIND=JPIM), ALLOCATABLE  :: IGP3ALEVS(:)
  INTEGER(KIND=JPIM), ALLOCATABLE  :: IGP3BPARS(:)
  INTEGER(KIND=JPIM), ALLOCATABLE  :: IGP3BLEVS(:)
  CONTAINS
    PROCEDURE :: ALLOCATE_TOG_VARS
END TYPE TRXTOY_VARS

CONTAINS 

SUBROUTINE ALLOCATE_CTX(SELF, NPROC,NLENGTF, NGPBLKS,NPRTRNS)
  CLASS(TRXTOY_CTX), INTENT(INOUT) :: SELF
  INTEGER(KIND=JPIM), INTENT(IN) :: NPROC,NLENGTF,NGPBLKS,NPRTRNS
    
  ALLOCATE (SELF%ISENDTOT (NPROC))
  ALLOCATE (SELF%IRECVTOT (NPROC))
  ALLOCATE (SELF%ISEND    (NPROC))
  ALLOCATE (SELF%IRECV    (NPROC))
  ALLOCATE (SELF%IINDEX(NLENGTF))
  ALLOCATE (SELF%INDOFF(NPROC))
  ALLOCATE (SELF%IGPTRSEND(2,NGPBLKS,NPRTRNS))
  ALLOCATE (SELF%ISETAL(NPROC))
  ALLOCATE (SELF%ISETBL(NPROC))
  ALLOCATE (SELF%ISETWL(NPROC))
  ALLOCATE (SELF%ISETVL(NPROC))
END SUBROUTINE ALLOCATE_CTX

SUBROUTINE ALLOCATE_TOG_VARS(SELF, KF_GP, NGPBLKS)
  CLASS(TRXTOY_VARS), INTENT(INOUT) :: SELF
  INTEGER(KIND=JPIM), INTENT(IN) :: KF_GP
  INTEGER(KIND=JPIM), INTENT(IN) :: NGPBLKS

  ALLOCATE(SELF%IUVLEVS(KF_GP))
  ALLOCATE(SELF%IUVPARS(KF_GP))
  ALLOCATE(SELF%IGP2PARS(KF_GP))
  ALLOCATE(SELF%IFLDOFF(KF_GP))  
  ALLOCATE(SELF%IGPTROFF(NGPBLKS))    
  ALLOCATE(SELF%LLUV(KF_GP))
  ALLOCATE(SELF%LLGP2(KF_GP))
  ALLOCATE(SELF%LLGP3A(KF_GP))
  ALLOCATE(SELF%LLGP3B(KF_GP))
  ALLOCATE(SELF%IGP3APARS(KF_GP))
  ALLOCATE(SELF%IGP3ALEVS(KF_GP))  
  ALLOCATE(SELF%IGP3BPARS(KF_GP))
  ALLOCATE(SELF%IGP3BLEVS(KF_GP))  
  
END SUBROUTINE ALLOCATE_TOG_VARS

SUBROUTINE ALLOCATE_HEAP_BUFFER(Z_HEAP, S1, S2)
  USE PARKIND1  ,ONLY : JPIM, JPRB

  IMPLICIT NONE

  REAL(KIND=JPRB), INTENT(INOUT), ALLOCATABLE :: Z_HEAP(:,:)
  INTEGER(KIND=JPIM), INTENT(IN) :: S1, S2  

  IF (ALLOCATED(Z_HEAP) .AND. (S1 /= SIZE(Z_HEAP,1) .OR. S2 /= SIZE(Z_HEAP,2) )) THEN
    DEALLOCATE(Z_HEAP)
  ENDIF

  IF (.NOT. ALLOCATED(Z_HEAP)) THEN
    ALLOCATE(Z_HEAP(-1:S1,S2))
  ENDIF

END SUBROUTINE ALLOCATE_HEAP_BUFFER


SUBROUTINE INIT_TOG_VARS(YDTOGLVARS, KF_SCALARS_G, &
                       & PGP, PGPUV, PGP3A, PGP3B, PGP2)
  USE PARKIND1  ,ONLY : JPIM, JPRB
  USE TPM_TRANS       ,ONLY : LDIVGP, LSCDERS, LUVDER, LVORGP
  IMPLICIT NONE
  
  TYPE(TRXTOY_VARS), INTENT(INOUT) :: YDTOGLVARS
  INTEGER(KIND=JPIM), INTENT(IN) :: KF_SCALARS_G  
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)  :: PGP(:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)  :: PGPUV(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)  :: PGP3A(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)  :: PGP3B(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)  :: PGP2(:,:,:)
    
  ! Local variables
  LOGICAL   :: LLPGPUV,LLPGP3A,LLPGP3B,LLPGP2     
  INTEGER(KIND=JPIM) :: IUVPAR,IUVLEV,IGP2PAR,IGP3ALEV,IGP3APAR,IGP3BLEV,IGP3BPAR,IPAROFF
  INTEGER(KIND=JPIM) :: IOFF,IOFF1,IOFFNS,IOFFEW,J1,J2
  
  INTEGER(KIND=JPIM) ::  J
    
  ASSOCIATE(IUVLEVS=>YDTOGLVARS%IUVLEVS, IUVPARS=>YDTOGLVARS%IUVPARS, &
            IGP2PARS=>YDTOGLVARS%IGP2PARS, LLUV=>YDTOGLVARS%LLUV, &
            LLGP2=>YDTOGLVARS%LLGP2, LLGP3A=>YDTOGLVARS%LLGP3A, &
            LLGP3B=>YDTOGLVARS%LLGP3B, IGP3APARS=>YDTOGLVARS%IGP3APARS, &
            IGP3ALEVS=>YDTOGLVARS%IGP3ALEVS, IGP3BPARS=>YDTOGLVARS%IGP3BPARS, &
            IGP3BLEVS=>YDTOGLVARS%IGP3BLEVS)
  LLPGPUV = .FALSE.
  LLPGP3A = .FALSE.
  LLPGP3B = .FALSE.
  LLPGP2  = .FALSE.
  
  IF(PRESENT(PGPUV))   LLPGPUV=.TRUE.
  IF(PRESENT(PGP3A))   LLPGP3A=.TRUE.
  IF(PRESENT(PGP3B))   LLPGP3B=.TRUE.
  IF(PRESENT(PGP2))    LLPGP2=.TRUE.
  
  IUVPAR=0
  IUVLEV=0
  IOFF1=0
  IOFFNS=KF_SCALARS_G
  IOFFEW=2*KF_SCALARS_G

  LLUV(:) = .FALSE.
  IUVPARS(:) = -99
  IUVLEVS(:) = -99

  IF (LLPGPUV) THEN
  IOFF=0
  IUVLEV=UBOUND(PGPUV,2)
  IF(LVORGP) THEN
    IUVPAR=IUVPAR+1
    DO J=1,IUVLEV
      IUVLEVS(IOFF+J)=J
      IUVPARS(IOFF+J)=IUVPAR
      LLUV(IOFF+J)=.TRUE.
    ENDDO
    IOFF=IOFF+IUVLEV
  ENDIF
  IF(LDIVGP) THEN
    IUVPAR=IUVPAR+1
    DO J=1,IUVLEV
      IUVLEVS(IOFF+J)=J
      IUVPARS(IOFF+J)=IUVPAR
      LLUV(IOFF+J)=.TRUE.
    ENDDO
    IOFF=IOFF+IUVLEV
  ENDIF
  DO J=1,IUVLEV
    IUVLEVS(IOFF+J)=J
    IUVPARS(IOFF+J)=IUVPAR+1
    IUVLEVS(IOFF+J+IUVLEV)=J
    IUVPARS(IOFF+J+IUVLEV)=IUVPAR+2
  ENDDO
  IUVPAR=IUVPAR+2
  LLUV(IOFF+1:IOFF+2*IUVLEV)=.TRUE.
  IOFF=IOFF+2*IUVLEV
  IOFF1=IOFF
  IOFFNS=IOFFNS+IOFF
  IOFFEW=IOFFEW+IOFF

  IOFF=IUVPAR*IUVLEV+KF_SCALARS_G
  IF(LUVDER) THEN
    IF(LSCDERS) IOFF=IOFF+KF_SCALARS_G
    DO J=1,IUVLEV
      IUVLEVS(IOFF+J)=J
      IUVPARS(IOFF+J)=IUVPAR+1
      LLUV(IOFF+J)=.TRUE.
      IUVLEVS(IOFF+J+IUVLEV)=J
      IUVPARS(IOFF+J+IUVLEV)=IUVPAR+2
      LLUV(IOFF+J+IUVLEV)=.TRUE.
    ENDDO
    IUVPAR=IUVPAR+2
    IOFF=IOFF+2*IUVLEV
    IOFFEW=IOFFEW+2*IUVLEV
  ENDIF
ENDIF

LLGP2(:)=.FALSE.
IF(LLPGP2) THEN
  IOFF=IOFF1
  IGP2PAR=UBOUND(PGP2,2)
  IF(LSCDERS) IGP2PAR=IGP2PAR/3
  DO J=1,IGP2PAR
    LLGP2(J+IOFF) = .TRUE.
    IGP2PARS(J+IOFF)=J
  ENDDO
  IOFF1=IOFF1+IGP2PAR
  IF(LSCDERS) THEN
    IOFF=IOFFNS
    DO J=1,IGP2PAR
      LLGP2(J+IOFF) = .TRUE.
      IGP2PARS(J+IOFF)=J+IGP2PAR
    ENDDO
    IOFFNS=IOFF+IGP2PAR
    IOFF=IOFFEW
    DO J=1,IGP2PAR
      LLGP2(J+IOFF) = .TRUE.
      IGP2PARS(J+IOFF)=J+2*IGP2PAR
    ENDDO
    IOFFEW=IOFF+IGP2PAR
  ENDIF
ENDIF

LLGP3A(:) = .FALSE.
IF(LLPGP3A) THEN
  IGP3ALEV=UBOUND(PGP3A,2)
  IGP3APAR=UBOUND(PGP3A,3)
  IF(LSCDERS) IGP3APAR=IGP3APAR/3
  IOFF=IOFF1
  DO J1=1,IGP3APAR
    DO J2=1,IGP3ALEV
      LLGP3A(J2+(J1-1)*IGP3ALEV+IOFF) = .TRUE.
      IGP3APARS(J2+(J1-1)*IGP3ALEV+IOFF)=J1
      IGP3ALEVS(J2+(J1-1)*IGP3ALEV+IOFF)=J2
    ENDDO
  ENDDO
  IPAROFF=IGP3APAR
  IOFF1=IOFF1+IGP3APAR*IGP3ALEV
  IF(LSCDERS) THEN
    IOFF=IOFFNS
    DO J1=1,IGP3APAR
      DO J2=1,IGP3ALEV
        LLGP3A(J2+(J1-1)*IGP3ALEV+IOFF) = .TRUE.
        IGP3APARS(J2+(J1-1)*IGP3ALEV+IOFF)=J1+IPAROFF
        IGP3ALEVS(J2+(J1-1)*IGP3ALEV+IOFF)=J2
      ENDDO
    ENDDO
    IPAROFF=IPAROFF+IGP3APAR
    IOFFNS=IOFFNS+IGP3APAR*IGP3ALEV
    IOFF=IOFFEW
    DO J1=1,IGP3APAR
      DO J2=1,IGP3ALEV
        LLGP3A(J2+(J1-1)*IGP3ALEV+IOFF) = .TRUE.
        IGP3APARS(J2+(J1-1)*IGP3ALEV+IOFF)=J1+IPAROFF
        IGP3ALEVS(J2+(J1-1)*IGP3ALEV+IOFF)=J2
      ENDDO
    ENDDO
    IOFFEW=IOFFEW+IGP3APAR*IGP3ALEV
  ENDIF
ENDIF

LLGP3B(:) = .FALSE.
IF(LLPGP3B) THEN
  IGP3BLEV=UBOUND(PGP3B,2)
  IGP3BPAR=UBOUND(PGP3B,3)
  IF(LSCDERS) IGP3BPAR=IGP3BPAR/3
  IOFF=IOFF1
  DO J1=1,IGP3BPAR
    DO J2=1,IGP3BLEV
      LLGP3B(J2+(J1-1)*IGP3BLEV+IOFF) = .TRUE.
      IGP3BPARS(J2+(J1-1)*IGP3BLEV+IOFF)=J1
      IGP3BLEVS(J2+(J1-1)*IGP3BLEV+IOFF)=J2
    ENDDO
  ENDDO
  IPAROFF=IGP3BPAR
  IOFF1=IOFF1+IGP3BPAR*IGP3BLEV
  IF(LSCDERS) THEN
    IOFF=IOFFNS
    DO J1=1,IGP3BPAR
      DO J2=1,IGP3BLEV
        LLGP3B(J2+(J1-1)*IGP3BLEV+IOFF) = .TRUE.
        IGP3BPARS(J2+(J1-1)*IGP3BLEV+IOFF)=J1+IPAROFF
        IGP3BLEVS(J2+(J1-1)*IGP3BLEV+IOFF)=J2
      ENDDO
    ENDDO
    IPAROFF=IPAROFF+IGP3BPAR
    IOFFNS=IOFFNS+IGP3BPAR*IGP3BLEV
    IOFF=IOFFEW
    DO J1=1,IGP3BPAR
      DO J2=1,IGP3BLEV
        LLGP3B(J2+(J1-1)*IGP3BLEV+IOFF) = .TRUE.
        IGP3BPARS(J2+(J1-1)*IGP3BLEV+IOFF)=J1+IPAROFF
        IGP3BLEVS(J2+(J1-1)*IGP3BLEV+IOFF)=J2
      ENDDO
    ENDDO
    IOFFEW=IOFFEW+IGP3BPAR*IGP3BLEV
  ENDIF
ENDIF
END ASSOCIATE
END SUBROUTINE INIT_TOG_VARS

SUBROUTINE INIT_TOG_OFF_VARS(YDCTX,YDTOGLVARS,KVSET,KPTRGP,KF_GP,NGPBLKS)
  USE TPM_DISTR       ,ONLY : MYSETV, MYSETW
  
  TYPE(TRXTOY_CTX),  INTENT(INOUT) :: YDCTX
  TYPE(TRXTOY_VARS), INTENT(INOUT) :: YDTOGLVARS
  INTEGER(KIND=JPIM), INTENT(IN) :: KVSET(:)
  INTEGER(KIND=JPIM) ,OPTIONAL, INTENT(IN) :: KPTRGP(:)  
  INTEGER(KIND=JPIM), INTENT(IN) :: KF_GP
  INTEGER(KIND=JPIM), INTENT(IN) :: NGPBLKS
  !local variables
  INTEGER(KIND=JPIM) :: JFLD, IFLDS, IFIRST, ILAST,IPOS, JBLK
  LOGICAL :: LLINDER
  ASSOCIATE(KGPTRSEND =>YDCTX%IGPTRSEND)
  ASSOCIATE(IFLDOFF=>YDTOGLVARS%IFLDOFF, IGPTROFF=>YDTOGLVARS%IGPTROFF)
  IFLDS = 0
  DO JFLD=1,KF_GP
    IF(KVSET(JFLD) == MYSETV .OR. KVSET(JFLD) == -1) THEN
      IFLDS = IFLDS+1
      IF(LLINDER) THEN
        IFLDOFF(IFLDS) = KPTRGP(JFLD)
      ELSE
        IFLDOFF(IFLDS) = JFLD
      ENDIF
    ENDIF
  ENDDO

  IPOS=0
  DO JBLK=1,NGPBLKS
    IGPTROFF(JBLK)=IPOS
    IFIRST = KGPTRSEND(1,JBLK,MYSETW)
    IF(IFIRST > 0) THEN
      ILAST = KGPTRSEND(2,JBLK,MYSETW)
      IPOS=IPOS+ILAST-IFIRST+1
    ENDIF
  ENDDO
  END ASSOCIATE
  END ASSOCIATE
END SUBROUTINE INIT_TOG_OFF_VARS

SUBROUTINE COPY_ZCOMBUF(LSEND, YDCTX, YDTOGLVARS, INRS,&
                      & ZCOMBUF,NGPBLKS, LLPGPONLY, LLINDER,&
                      & IPOSPLUS, IFLDA,ISETW,JPOS,&
                      & KPTRGP,&
                      & PGP, PGPUV, PGP3A, PGP3B, PGP2,&
                      & PGP_RDLY, PGPUV_RDLY, PGP3A_RDLY, PGP3B_RDLY, PGP2_RDLY)
 
  USE PARKIND1  ,ONLY : JPIM, JPRB
  
  LOGICAL, INTENT(IN) :: LSEND 
  TYPE(TRXTOY_CTX),  INTENT(INOUT) :: YDCTX
  TYPE(TRXTOY_VARS), INTENT(INOUT) :: YDTOGLVARS
  INTEGER(KIND=JPIM), INTENT(IN) :: NGPBLKS
  INTEGER(KIND=JPIM), INTENT(IN) :: INRS
  REAL(KIND=JPRB), POINTER, CONTIGUOUS, INTENT(INOUT) :: ZCOMBUF(:,:)  
  LOGICAL, INTENT(IN)   :: LLPGPONLY, LLINDER
  INTEGER(KIND=JPIM), INTENT(IN) :: IPOSPLUS(:)
  INTEGER(KIND=JPIM), INTENT(IN) :: IFLDA(:,:)
  INTEGER(KIND=JPIM), INTENT(IN) :: ISETW(:)
  INTEGER(KIND=JPIM), INTENT(IN) :: JPOS(:,:)  
  INTEGER(KIND=JPIM) ,OPTIONAL, INTENT(IN) :: KPTRGP(:)  
  REAL(KIND=JPRB),OPTIONAL,INTENT(OUT)     :: PGP(:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(OUT)     :: PGPUV(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(OUT)     :: PGP3A(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(OUT)     :: PGP3B(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(OUT)     :: PGP2(:,:,:)
  
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP_RDLY(:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGPUV_RDLY(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP3A_RDLY(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP3B_RDLY(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP2_RDLY(:,:,:)
  
  !Local variables
  INTEGER(KIND=JPIM) :: I_FLD_START,I_FLD_END
  INTEGER(KIND=JPIM) :: IFIRST, ILAST
  INTEGER(KIND=JPIM) :: JJ,JI,JK,IFLDT, JBLK, IPOS

  ASSOCIATE(IUVLEVS=>YDTOGLVARS%IUVLEVS, IUVPARS=>YDTOGLVARS%IUVPARS, &
            IGP2PARS=>YDTOGLVARS%IGP2PARS, LLUV=>YDTOGLVARS%LLUV, &
            LLGP2=>YDTOGLVARS%LLGP2, LLGP3A=>YDTOGLVARS%LLGP3A, &
            LLGP3B=>YDTOGLVARS%LLGP3B, IGP3APARS=>YDTOGLVARS%IGP3APARS, &
            IGP3ALEVS=>YDTOGLVARS%IGP3ALEVS, IGP3BPARS=>YDTOGLVARS%IGP3BPARS, &
            IGP3BLEVS=>YDTOGLVARS%IGP3BLEVS)
  ASSOCIATE (KSENDCOUNT=>YDCTX%ISENDCOUNT, KRECVCOUNT=>YDCTX%IRECVCOUNT, KNSEND=>YDCTX%INSEND, KNRECV=>YDCTX%INRECV, &
          & KSENDTOT=>YDCTX%ISENDTOT, KRECVTOT=>YDCTX%IRECVTOT, KSEND=>YDCTX%ISEND, KRECV=>YDCTX%IRECV, KINDEX=>YDCTX%IINDEX, &
          & KNDOFF=>YDCTX%INDOFF, KGPTRSEND =>YDCTX%IGPTRSEND, KSETAL=>YDCTX%ISETAL, KSETBL=>YDCTX%ISETBL, KSETWL=>YDCTX%ISETWL, &
          & KSETVL=>YDCTX%ISETVL)

  IPOS=IPOSPLUS(INRS)
  I_FLD_START = ZCOMBUF(-1,INRS)
  I_FLD_END   = ZCOMBUF(0,INRS)
  
  !$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(IFLDT,IFIRST,ILAST,JK,JJ,JI,JBLK)
  DO JJ=I_FLD_START,I_FLD_END
    IFLDT=IFLDA(JJ,INRS)
    DO JBLK=1,NGPBLKS
      IFIRST = KGPTRSEND(1,JBLK,ISETW(INRS))
      IF(IFIRST > 0) THEN
        ILAST = KGPTRSEND(2,JBLK,ISETW(INRS))
        IF(LLINDER) THEN
          DO JK=IFIRST,ILAST
            JI=(JJ-I_FLD_START)*IPOS+JPOS(JBLK,INRS)+JK-IFIRST+1
            IF (LSEND) THEN
              PGP(JK,KPTRGP(IFLDT),JBLK) = ZCOMBUF(JI,INRS)
            ELSE
              ZCOMBUF(JI,INRS) = PGP_RDLY(JK,KPTRGP(IFLDT),JBLK)
            ENDIF
          ENDDO
        ELSEIF(LLPGPONLY) THEN
          DO JK=IFIRST,ILAST
            JI=(JJ-I_FLD_START)*IPOS+JPOS(JBLK,INRS)+JK-IFIRST+1
            IF (LSEND) THEN
              PGP(JK,IFLDT,JBLK) = ZCOMBUF(JI,INRS)
            ELSE
              ZCOMBUF(JI,INRS) = PGP_RDLY(JK,IFLDT,JBLK)
            ENDIF
          ENDDO
        ELSEIF(LLUV(IFLDT)) THEN
          DO JK=IFIRST,ILAST
            JI=(JJ-I_FLD_START)*IPOS+JPOS(JBLK,INRS)+JK-IFIRST+1
            IF (LSEND) THEN
              PGPUV(JK,IUVLEVS(IFLDT),IUVPARS(IFLDT),JBLK) = ZCOMBUF(JI,INRS)
            ELSE
              ZCOMBUF(JI,INRS) = PGPUV_RDLY(JK,IUVLEVS(IFLDT),IUVPARS(IFLDT),JBLK)
            ENDIF
          ENDDO
        ELSEIF(LLGP2(IFLDT)) THEN
          DO JK=IFIRST,ILAST
            JI=(JJ-I_FLD_START)*IPOS+JPOS(JBLK,INRS)+JK-IFIRST+1
            IF (LSEND) THEN
              PGP2(JK,IGP2PARS(IFLDT),JBLK) = ZCOMBUF(JI,INRS)
            ELSE
              ZCOMBUF(JI,INRS) = PGP2_RDLY(JK,IGP2PARS(IFLDT),JBLK)
            ENDIF
          ENDDO
        ELSEIF(LLGP3A(IFLDT)) THEN
          DO JK=IFIRST,ILAST
            JI=(JJ-I_FLD_START)*IPOS+JPOS(JBLK,INRS)+JK-IFIRST+1
            IF (LSEND) THEN
              PGP3A(JK,IGP3ALEVS(IFLDT),IGP3APARS(IFLDT),JBLK) = ZCOMBUF(JI,INRS)
            ELSE
              ZCOMBUF(JI,INRS) = PGP3A_RDLY(JK,IGP3ALEVS(IFLDT),IGP3APARS(IFLDT),JBLK)
            ENDIF
          ENDDO
        ELSEIF(LLGP3B(IFLDT)) THEN
          DO JK=IFIRST,ILAST
            JI=(JJ-I_FLD_START)*IPOS+JPOS(JBLK,INRS)+JK-IFIRST+1
            IF (LSEND) THEN
              PGP3B(JK,IGP3BLEVS(IFLDT),IGP3BPARS(IFLDT),JBLK) = ZCOMBUF(JI,INRS)
            ELSE
              ZCOMBUF(JI,INRS) = PGP3B_RDLY(JK,IGP3BLEVS(IFLDT),IGP3BPARS(IFLDT),JBLK)
            ENDIF
          ENDDO
        ENDIF
      ENDIF
    ENDDO
  ENDDO
  !$OMP END PARALLEL DO
END ASSOCIATE
END ASSOCIATE
END SUBROUTINE COPY_ZCOMBUF


SUBROUTINE COPY_PGLAT(LSEND,YDCTX, YDTOGLVARS, NGPBLKS,IFLDS, LLPGPONLY, LLINDER,&                    
                    & PGP, PGPUV, PGP3A, PGP3B, PGP2,&
                    & PGP_RDLY, PGPUV_RDLY, PGP3A_RDLY, PGP3B_RDLY, PGP2_RDLY,&
                    & PGLAT, PGLAT_RDLY)

  USE PARKIND1  ,ONLY : JPIM, JPRB, JPIB
  USE TPM_DISTR       ,ONLY : MYSETW, MYPROC
  USE TPM_GEN         ,ONLY : NOUT
  USE ABORT_TRANS_MOD ,ONLY : ABORT_TRANS

  LOGICAL, INTENT(IN) :: LSEND 
  TYPE(TRXTOY_CTX),  INTENT(INOUT) :: YDCTX
  TYPE(TRXTOY_VARS), INTENT(INOUT) :: YDTOGLVARS
  
  INTEGER(KIND=JPIM), INTENT(IN) :: NGPBLKS
  INTEGER(KIND=JPIM), INTENT(IN) :: IFLDS
  LOGICAL, INTENT(IN)   :: LLPGPONLY, LLINDER
  
  REAL(KIND=JPRB),OPTIONAL,INTENT(OUT)     :: PGP(:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(OUT)     :: PGPUV(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(OUT)     :: PGP3A(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(OUT)     :: PGP3B(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(OUT)     :: PGP2(:,:,:)
  
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP_RDLY(:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGPUV_RDLY(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP3A_RDLY(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP3B_RDLY(:,:,:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)     :: PGP2_RDLY(:,:,:)
  
  REAL(KIND=JPRB),OPTIONAL,INTENT(OUT)   :: PGLAT(:,:)
  REAL(KIND=JPRB),OPTIONAL,INTENT(IN)    :: PGLAT_RDLY(:,:)
  
  !Local variables
  INTEGER(KIND=JPIM) :: IFIRST, ILAST, IFLD, IPOS, JBLK, JK
  INTEGER(KIND=JPIB) :: JFLD64
  ASSOCIATE(IUVLEVS=>YDTOGLVARS%IUVLEVS,IFLDOFF=>YDTOGLVARS%IFLDOFF,&
            IGPTROFF=>YDTOGLVARS%IGPTROFF,&
            IUVPARS=>YDTOGLVARS%IUVPARS, &
            IGP2PARS=>YDTOGLVARS%IGP2PARS, LLUV=>YDTOGLVARS%LLUV, &
            LLGP2=>YDTOGLVARS%LLGP2, LLGP3A=>YDTOGLVARS%LLGP3A, &
            LLGP3B=>YDTOGLVARS%LLGP3B, IGP3APARS=>YDTOGLVARS%IGP3APARS, &
            IGP3ALEVS=>YDTOGLVARS%IGP3ALEVS, IGP3BPARS=>YDTOGLVARS%IGP3BPARS, &
            IGP3BLEVS=>YDTOGLVARS%IGP3BLEVS)
  ASSOCIATE (KSENDCOUNT=>YDCTX%ISENDCOUNT, KRECVCOUNT=>YDCTX%IRECVCOUNT, KNSEND=>YDCTX%INSEND, KNRECV=>YDCTX%INRECV, &
          & KSENDTOT=>YDCTX%ISENDTOT, KRECVTOT=>YDCTX%IRECVTOT, KSEND=>YDCTX%ISEND, KRECV=>YDCTX%IRECV, KINDEX=>YDCTX%IINDEX, &
          & KNDOFF=>YDCTX%INDOFF, KGPTRSEND =>YDCTX%IGPTRSEND, KSETAL=>YDCTX%ISETAL, KSETBL=>YDCTX%ISETBL, KSETWL=>YDCTX%ISETWL, &
          & KSETVL=>YDCTX%ISETVL)

#ifdef __NEC__
  ! Loops inversion is still better on Aurora machines, according to CHMI. REK.
  !$OMP PARALLEL DO SCHEDULE(DYNAMIC) PRIVATE(JFLD64,JBLK,JK,IFLD,IPOS,IFIRST,ILAST)
#else
  !$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JFLD64,JBLK,JK,IFLD,IPOS,IFIRST,ILAST)
#endif
  DO JBLK=1,NGPBLKS
    IFIRST = KGPTRSEND(1,JBLK,MYSETW)
    IF(IFIRST > 0) THEN
      ILAST = KGPTRSEND(2,JBLK,MYSETW)
      ! Address PGLAT over 64 bits because its size may exceed 2 GB for big data and
      ! small number of tasks.
      IF(LLPGPONLY) THEN
        DO JFLD64=1,IFLDS
          IFLD = IFLDOFF(JFLD64)
          !DIR$ VECTOR ALWAYS
          DO JK=IFIRST,ILAST
            IPOS = KNDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
            IF (LSEND) THEN
              PGP(JK,IFLD,JBLK) = PGLAT(JFLD64,KINDEX(IPOS))
            ELSE
              PGLAT(JFLD64,KINDEX(IPOS)) = PGP(JK,IFLD,JBLK)
            ENDIF
          ENDDO
        ENDDO
      ELSE
        DO JFLD64=1,IFLDS
          IFLD = IFLDOFF(JFLD64)
          IF(LLUV(IFLD)) THEN
            !DIR$ VECTOR ALWAYS
            DO JK=IFIRST,ILAST
              IPOS = KNDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
              IF (LSEND) THEN  
                PGPUV(JK,IUVLEVS(IFLD),IUVPARS(IFLD),JBLK) = PGLAT(JFLD64,KINDEX(IPOS))
              ELSE
                PGLAT(JFLD64,KINDEX(IPOS)) = PGPUV(JK,IUVLEVS(IFLD),IUVPARS(IFLD),JBLK)
              ENDIF
            ENDDO
          ELSEIF(LLGP2(IFLD)) THEN
            !DIR$ VECTOR ALWAYS
            DO JK=IFIRST,ILAST
              IPOS = KNDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
              IF (LSEND) THEN  
                PGP2(JK,IGP2PARS(IFLD),JBLK)=PGLAT(JFLD64,KINDEX(IPOS))
              ELSE
                PGLAT(JFLD64,KINDEX(IPOS)) = PGP2(JK,IGP2PARS(IFLD),JBLK)
              ENDIF
            ENDDO
          ELSEIF(LLGP3A(IFLD)) THEN
            !DIR$ VECTOR ALWAYS
            DO JK=IFIRST,ILAST
              IPOS = KNDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
              IF (LSEND) THEN  
                PGP3A(JK,IGP3ALEVS(IFLD),IGP3APARS(IFLD),JBLK)=PGLAT(JFLD64,KINDEX(IPOS))
              ELSE
                PGLAT(JFLD64,KINDEX(IPOS)) = PGP3A(JK,IGP3ALEVS(IFLD),IGP3APARS(IFLD),JBLK)
              ENDIF
            ENDDO
          ELSEIF(LLGP3B(IFLD)) THEN
            !DIR$ VECTOR ALWAYS
            DO JK=IFIRST,ILAST
              IPOS = KNDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
              IF (LSEND) THEN
                PGP3B(JK,IGP3BLEVS(IFLD),IGP3BPARS(IFLD),JBLK)=PGLAT(JFLD64,KINDEX(IPOS))  
              ELSE
                PGLAT(JFLD64,KINDEX(IPOS)) = PGP3B(JK,IGP3BLEVS(IFLD),IGP3BPARS(IFLD),JBLK)
              ENDIF
            ENDDO
          ELSE
            WRITE(NOUT,*)'TRGTOL_MOD: ERROR',JFLD64,IFLD
            CALL ABORT_TRANS('TRGTOL_MOD: ERROR')
          ENDIF
        ENDDO
      ENDIF
    ENDIF
  ENDDO
  !$OMP END PARALLEL DO

END ASSOCIATE
END ASSOCIATE
END SUBROUTINE COPY_PGLAT


END MODULE TRXTOY_CTX_MOD
